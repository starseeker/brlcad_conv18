#                A S T Y L E . C M A K E . I N
# BRL-CAD
#
# Copyright (c) 2012-2013 United States Government as represented by
# the U.S. Army Research Laboratory.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following
# disclaimer in the documentation and/or other materials provided
# with the distribution.
#
# 3. The name of the author may not be used to endorse or promote
# products derived from this software without specific prior written
# permission.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS
# OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
# GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
# NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
###

set(srcslist @srcslist_tmp@)
set(target_file @lib_outfile@)
set(binary_dir @CMAKE_CURRENT_BINARY_DIR@)
set(src_dir @CMAKE_CURRENT_SOURCE_DIR@)
set(check_srcs)
foreach(srcfile ${srcslist})
  get_filename_component(root_name ${srcfile} NAME_WE)
  string(MD5 path_md5 "${src_dir}/${srcfile}")
  file(MD5 ${src_dir}/${srcfile} CURR_MD5)
  if(EXISTS ${binary_dir}/validation/${root_name}_${path_md5}.md5)
    file(READ ${binary_dir}/validation/${root_name}_${path_md5}.md5 OLD_MD5)
    string(STRIP "${OLD_MD5}" OLD_MD5)
    if(NOT "${CURR_MD5}" STREQUAL "${OLD_MD5}")
      file(WRITE ${binary_dir}/validation/${root_name}_${path_md5}.md5 ${CURR_MD5})
      set(check_srcs ${check_srcs} ${srcfile})
    endif(NOT "${CURR_MD5}" STREQUAL "${OLD_MD5}")
  else(EXISTS ${binary_dir}/validation/${root_name}_${path_md5}.md5)
    file(WRITE ${binary_dir}/validation/${root_name}_${path_md5}.md5 ${CURR_MD5})
    set(check_srcs ${check_srcs} ${srcfile})
  endif(EXISTS ${binary_dir}/validation/${root_name}_${path_md5}.md5)
endforeach(srcfile ${srcslist})

set(diff_files)
foreach(srcfile ${check_srcs})
  get_filename_component(root_name ${srcfile} NAME_WE)
  string(MD5 path_md5 "${src_dir}/${srcfile}")
  set(working_file "${binary_dir}/${root_name}_${path_md5}")
  execute_process(COMMAND "@CMAKE_COMMAND@" -E copy "${src_dir}/${src_file}" "${working_file}")
  execute_process(COMMAND "@ASTYLE_EXECUTABLE@" --options=@BRLCAD_SOURCE_DIR@/misc/astyle.opt "${working_file}" OUTPUT_QUIET ERROR_QUIET)
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${working_file}.orig")
  execute_process(COMMAND "@CMAKE_COMMAND@" -E compare_files  "${src_dir}/${srcfile}" "${working_file}" RESULT_VARIABLE CMDRESULT OUTPUT_QUIET ERROR_QUIET)
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${working_file}")
  if(CMDRESULT)
    set(diff_files ${diff_files} "${src_dir}/${srcfile}")
  endif(CMDRESULT)
endforeach(srcfile ${check_srcs})

if(diff_files)
  set(out_msg "Style verification failure in the following file(s):\n")
  foreach(formatting_diff ${diff_files})
    set(out_msg "${out_msg}  ${formatting_diff}\n")
  endforeach(formatting_diff ${diff_files})
  execute_process(COMMAND "@CMAKE_COMMAND@" -E remove -f "${target_file}")
  message(FATAL_ERROR "${out_msg}")
endif(diff_files)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8
