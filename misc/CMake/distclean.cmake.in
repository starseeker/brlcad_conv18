# Clear benchmark files, if present
file(GLOB bench_files RELATIVE "@BRLCAD_BINARY_DIR@/bench" "@BRLCAD_BINARY_DIR@/bench/*")
if("${bench_files}" MATCHES "pix" OR "${bench_files}" MATCHES "log")
  execute_process(COMMAND ../@BIN_DIR@/benchmark clobber WORKING_DIRECTORY "@BRLCAD_BINARY_DIR@/bench")
endif("${bench_files}" MATCHES "pix" OR "${bench_files}" MATCHES "log")

# Clear out the files the BRL-CAD build told us to clear
message("Removing CMake-generated files...")
set(clear_files "@CMAKE_DISTCLEAN_TARGET_LIST@")
file(REMOVE_RECURSE ${clear_files})
# Clean up after CPack
if(EXISTS "@BRLCAD_BINARY_DIR@/_CPack_Packages")
  file(REMOVE_RECURSE "@BRLCAD_BINARY_DIR@/_CPackPackages")
endif(EXISTS "@BRLCAD_BINARY_DIR@/_CPack_Packages")
if(EXISTS "@BRLCAD_BINARY_DIR@/source_archive_contents")
  file(REMOVE_RECURSE "@BRLCAD_BINARY_DIR@/source_archive_contents")
endif(EXISTS "@BRLCAD_BINARY_DIR@/source_archive_contents")
message("Removing CMake-generated files... done.")

# Remove any empty directories - use CMake rather than 
# find for portability - also, find was causing problems
# with filenames using spaces.
message("Removing empty directories in the build directory...")
file(GLOB remaining_files "@BRLCAD_BINARY_DIR@/*")
set(dir_list)
foreach(candidate ${remaining_files})
  if(IS_DIRECTORY "${candidate}")
    set(dir_list ${dir_list} "${candidate}")
  endif(IS_DIRECTORY "${candidate}")
endforeach(candidate ${remaining_files})
set(HAVE_EMPTY_DIRS 1)
while(HAVE_EMPTY_DIRS)
  set(HAVE_EMPTY_DIRS 0)
  foreach(candidate ${dir_list})
    set(dir_contents)
    file(GLOB dir_contents "${candidate}/*")
    if(NOT dir_contents)
      list(REMOVE_ITEM dir_list "${candidate}")
      file(REMOVE_RECURSE "${candidate}")
      set(HAVE_EMPTY_DIRS 1)
    else(NOT dir_contents)
      foreach(newfind ${dir_contents})
	if(IS_DIRECTORY "${newfind}")
	  set(dir_list ${dir_list} "${newfind}")
	endif(IS_DIRECTORY "${newfind}")
      endforeach(newfind ${dir_contents})
    endif(NOT dir_contents)
  endforeach(candidate ${dir_list})
endwhile(HAVE_EMPTY_DIRS)
message("Removing empty directories in the build directory... done.")

# Done - now remove the output form of this script
file(REMOVE "@BRLCAD_BINARY_DIR@/distclean.cmake")
